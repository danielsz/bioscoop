filtergraph    = sws-flags? filterchain (<semicolon> filterchain)*
sws-flags      = <'sws_flags'> <equals> flags <semicolon>
flags          = #'[^;]+'


filterchain    = filter (<comma> filter)*
filter         = input-linklabels? filter-spec output-linklabels?

input-linklabels  = linklabel+
output-linklabels = linklabel+
linklabel      = <lbracket> name <rbracket>

filter-spec    = filter-name (<equals> filter-arguments)?
filter-name    = name (at name)?

filter-arguments = (quoted-string | unquoted-args)+
unquoted-args   = #'[^,;\s\[\]\'"]+'

quoted-string  = <"'"> (escaped-char | #"[^']*") <"'">
escaped-char   = "\\\"" | "\\'" | "\\\\"


<name>         = #'[a-zA-Z0-9_:]+'
at           = '@'
equals       = '='
comma        = ','
semicolon    = ';'
lbracket     = '['
rbracket     = ']'

